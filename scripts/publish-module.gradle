apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
}

def libraryVersion = '2.1.0'
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifact androidSourcesJar
                groupId 'com.carterchen247'
                artifactId 'alarm-scheduler'
                version libraryVersion

                // ref: https://docs.gradle.org/current/userguide/publishing_maven.html#sec:modifying_the_generated_pom
                pom {
                    name = 'Alarm scheduler'
                    description = 'Alarm scheduler'
                    url = 'https://github.com/CarterChen247/AlarmScheduler'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://github.com/CarterChen247/AlarmScheduler'
                        }
                    }
                    developers {
                        developer {
                            id = 'carterchen247'
                            name = 'Carter Chen'
                            email = 'carterchen247@gmail.com'
                        }
                    }

                    // ref: https://maven.apache.org/pom.html#SCM
                    scm {
                        connection = 'scm:git:https://github.com/CarterChen247/AlarmScheduler.git'
                        developerConnection = 'scm:git:https://github.com/CarterChen247/AlarmScheduler.git'
                        url = 'https://github.com/CarterChen247/AlarmScheduler.git'
                    }
                }
            }
        }
    }
}

// ref: https://docs.gradle.org/current/userguide/signing_plugin.html#sec:signatory_credentials
ext["signing.keyId"] = rootProject.ext["signing.keyId"]
ext["signing.password"] = rootProject.ext["signing.password"]
ext["signing.secretKeyRingFile"] = rootProject.ext["signing.secretKeyRingFile"]

signing {
    sign publishing.publications
}
